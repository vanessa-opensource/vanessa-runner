///////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды init-project
//
// Структура модуля реализована в соответствии с рекомендациями
// oscript-app-template (C) EvilBeaver
//
///////////////////////////////////////////////////////////////////
#Использовать gui
#Использовать 1commands
#Использовать logos
///////////////////////////////////////////////////////////////////
Перем Настройки;
Перем Лог;
Перем ПакетыУстановки;
//Основная форма скрипта
Перем Форма;
//Стили закрепления (низ, вверх, лево, право)
Перем СтильЗакрепления;
//Индикатор создания проекта
Перем Индикатор;
///////////////////////////////////////////////////////////////////

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	
	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "     Создание структуры проекта.");
	
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--template", "Ссылка на шаблон");
	Парсер.ДобавитьКоманду(ОписаниеКоманды);
	
КонецПроцедуры // ЗарегистрироватьКоманду

// Выполняет логику команды
//
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   ДополнительныеПараметры - Соответствие - дополнительные параметры (необязательно)
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач ДополнительныеПараметры) Экспорт
	
	Попытка
		Лог = ДополнительныеПараметры.Лог;
	Исключение
		Лог = Логирование.ПолучитьЛог(ПараметрыСистемы.ИмяЛогаСистемы());
	КонецПопытки;
	
	ПрочитатьПараметры(ПараметрыКоманды);
	ИнициализацияПроекта();
	
	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;
	
КонецФункции // ВыполнитьКоманду

Процедура ПрочитатьПараметры(Знач ПараметрыКоманды)
	
	ПутьФайлаНастроек = ПараметрыКоманды["--settings"];
	
	Настройки = Новый Структура;
	
	Настройки.Вставить("СсылкаНаШаблон", ПараметрыКоманды["--template"]);
	Настройки.Вставить("ИспользуетсяФайлНастроек", ЗначениеЗаполнено(ПутьФайлаНастроек));
	
	Настройки.Вставить("ШаблонПроекта", ПараметрыКоманды["ШаблонПроекта"]);
	Настройки.Вставить("Пакеты", ПараметрыКоманды["Пакеты"]);
	
	ИспользоватьШаблон = Истина;
	Если ЗначениеЗаполнено(Настройки.ШаблонПроекта) Тогда
		ИспользоватьШаблон = Настройки.ШаблонПроекта.Получить("Использовать");
		Если ТипЗнч(ИспользоватьШаблон) <> Тип("Булево") Тогда
			ИспользоватьШаблон = Истина;
		КонецЕсли;
	КонецЕсли;
	Настройки.Вставить("ИспользоватьШаблон", ИспользоватьШаблон);
	
КонецПроцедуры

Процедура ИнициализацияПроекта()
	
	Лог.Информация("Создаю проект...");
	
	Если Настройки.ИспользоватьШаблон Тогда
		
		УспешноКлонирован = КлонироватьШаблон();
		Если НЕ УспешноКлонирован Тогда
			Лог.Информация("Не удалось клонировать шаблон");
			Возврат;
		КонецЕсли;
		
		Если НЕ Настройки.ИспользуетсяФайлНастроек Тогда
			Пакеты = ПрочитатьПакетыИзНастроекКлонированногоШаблона();
			Если ЗначениеЗаполнено(Пакеты) Тогда
				Настройки.Пакеты = Пакеты;
				Лог.Информация("Для установки выбраны пакеты из шаблона");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ИспользоватьПакетыПоУмолчанию = НЕ ЗначениеЗаполнено(Настройки.Пакеты);
	УстановитьПакеты(ИспользоватьПакетыПоУмолчанию);
	
	Лог.Информация("Проект создан");
	
КонецПроцедуры
///////////////////////////////////////////////////////////////////

Процедура УстановитьПакеты(ИспользоватьПакетыПоУмолчанию)
	
	Если ИспользоватьПакетыПоУмолчанию Тогда
		ПакетыУстановки = ПакетыПоУмолчанию();
		СоздатьФормуНастроек();
		Возврат;
	КонецЕсли;
	
	ПакетыУстановки = Настройки.Пакеты;
	Для Каждого СтрокаПакета Из ПакетыУстановки Цикл
		
		Пакет = ДанныеПакета(СтрокаПакета);
		Если НЕ Пакет.Использовать Тогда
			Продолжить;
		КонецЕсли;
		
		ВыполнитьКомандыУстановкиПакета(Пакет.Установка);
		
	КонецЦикла;
	
КонецПроцедуры

Функция КлонироватьШаблон()
	
	Шаблон = ШаблонПроекта();
	Лог.Информация("Использую шаблон проекта %1", Шаблон);
	
	ПараметрыКоманды = Новый Массив();
	ПараметрыКоманды.Добавить("clone");
	ПараметрыКоманды.Добавить(Шаблон);
	ПараметрыКоманды.Добавить(".");
	
	Возврат ВыполнитьКомандуПоСтрокеЗапуска("git", ПараметрыКоманды);
	
КонецФункции

Функция ПрочитатьПакетыИзНастроекКлонированногоШаблона()
	
	ПутьКФайлуНастроек = ОбъединитьПути(ТекущийКаталог(), ОбщиеМетоды.ИмяФайлаНастроек());
	
	НастройкиИзФайла = ОбщиеМетоды.ПрочитатьФайлJSON(ПутьКФайлуНастроек);
	
	НастройкиПроекта = НастройкиИзФайла.Получить(ИмяГруппыНастроек());
	
	Пакеты = Неопределено;
	Если ЗначениеЗаполнено(НастройкиПроекта) Тогда
		Пакеты = НастройкиПроекта.Получить("Пакеты");
	КонецЕсли;
	
	Возврат Пакеты;
	
КонецФункции

Функция ШаблонПроекта()
	
	Шаблон = "";
	Если ЗначениеЗаполнено(Настройки.СсылкаНаШаблон) Тогда
		Шаблон = Настройки.СсылкаНаШаблон;
	ИначеЕсли ЗначениеЗаполнено(Настройки.ШаблонПроекта) Тогда
		Шаблон = Настройки.ШаблонПроекта.Получить("Репозиторий");
	Иначе
		Шаблон = ШаблонПоУмолчанию();
	КонецЕсли;
	
	Возврат Шаблон;
	
КонецФункции

Процедура СоздатьФормуНастроек()
	
	УправляемыйИнтерфейс = Новый УправляемыйИнтерфейс();
	
	СтильЗакрепления = УправляемыйИнтерфейс.СтильЗакрепления;
	
	Форма = УправляемыйИнтерфейс.СоздатьФорму();
	Форма.СтартоваяПозиция = ПозицияОкнаПриОткрытии.ЦентрЭкрана;
	Форма.УстановитьДействие(ЭтотОбъект, "ПриОткрытии", "ПриОткрытииФормы");
	Форма.Показать();
	
КонецПроцедуры

Процедура ПриОткрытииФормы() Экспорт
	
	НастройкиФормы();
	
	СоздатьЭлементыБиблиотек();
	СоздатьЭлементыПодвала();
	
КонецПроцедуры

Процедура НастройкиФормы()
	
	Форма.Заголовок = "Настройки создания проекта";
	Форма.Ширина = 350;
	Форма.Высота = 200;
	
КонецПроцедуры

Процедура СоздатьЭлементыБиблиотек()
	
	ГруппаБиблиотек = Форма.Элементы.Добавить("ГруппаБиблиотек", "ГруппаФормы", Неопределено);
	ГруппаБиблиотек.Вид = Форма.ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаБиблиотек.Заголовок = "Выберите необходимые библиотеки для вашего проекта:";
	
	Для Каждого СтрокаПакета Из ПакетыУстановки Цикл
		
		Пакет = ДанныеПакета(СтрокаПакета);
		
		ПолеФлажок = Форма.Элементы.Добавить(Пакет.Имя, "ПолеФормы", ГруппаБиблиотек);
		ПолеФлажок.Вид = Форма.ВидПоляФормы.ПолеФлажка;
		ПолеФлажок.Заголовок = Пакет.Имя;
		ПолеФлажок.ПоложениеЗаголовка = Форма.ПоложениеЗаголовка.Право;
		ПолеФлажок.Значение = Пакет.Использовать;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьЭлементыПодвала()
	
	ГруппаПодвал = Форма.Элементы.Добавить("ГруппаПодвал", "ГруппаФормы", Неопределено);
	ГруппаПодвал.Вид = Форма.ВидГруппыФормы.ОбычнаяГруппа;
	
	Индикатор = Форма.Элементы.Добавить("ПрогрессБар", "ПолеФормы", ГруппаПодвал);
	Индикатор.Вид = Форма.ВидПоляФормы.ПолеИндикатора;
	Индикатор.Ширина = 220;
	Индикатор.Закрепление = СтильЗакрепления.Лево;
	
	КнопкаОК = Форма.Элементы.Добавить("КнопкаОК", "КнопкаФормы", ГруппаПодвал);
	КнопкаОК.Заголовок = "ОК";
	КнопкаОК.УстановитьДействие(ЭтотОбъект, "Нажатие", "УстановитьВыбранныеПакеты");
	
КонецПроцедуры

Процедура УстановитьВыбранныеПакеты() Экспорт
	
	Индикатор.Значение = 5;
	
	ГруппаБиблиотек = Форма.Элементы.Найти("ГруппаБиблиотек");
	ЭлементыБиблиотек = ГруппаБиблиотек.Элементы;
	
	Индикатор.Значение = 25;
	
	КоличествоЭлементов = ЭлементыБиблиотек.Количество();
	Итого = 100;
	ВесПакета = (Итого - Индикатор.Значение) / КоличествоЭлементов;
	
	Для Каждого СтрокаПакета Из ПакетыУстановки Цикл
		
		Пакет = ДанныеПакета(СтрокаПакета);
		Индикатор.Значение = Индикатор.Значение + ВесПакета;
		
		Значение = ЭлементыБиблиотек.Найти(Пакет.Имя).Значение;
		Если НЕ Значение Тогда
			Продолжить;
		КонецЕсли;
		
		ВыполнитьКомандыУстановкиПакета(Пакет.Установка);
		
	КонецЦикла;
	
	Форма.Закрыть();
	
КонецПроцедуры

Функция ИмяГруппыНастроек()
	Возврат ПараметрыСистемы.ВозможныеКоманды().СоздатьПроект;
КонецФункции

Функция ШаблонПоУмолчанию()
	Возврат "https://github.com/vanessa-opensource/vanessa-bootstrap.git";
КонецФункции

Функция ПакетыПоУмолчанию()
	
	Пакеты = Новый Массив();
	
	МассивКоманд = Новый Массив();
	МассивКоманд.Добавить("opm install gitsync");
	ПакетГитсинк = ПакетДляДобавления("gitsync", МассивКоманд, Истина);
	
	МассивКоманд = Новый Массив();
	МассивКоманд.Добавить("opm install gitrules");
	МассивКоманд.Добавить("gitrules install");
	ПакетГитрулес = ПакетДляДобавления("gitrules", МассивКоманд);
	
	МассивКоманд = Новый Массив();
	МассивКоманд.Добавить("opm install precommit1c");
	МассивКоманд.Добавить("precommit1c --install");
	ПакетПрекоммит = ПакетДляДобавления("precommit1c", МассивКоманд);
	
	Пакеты.Добавить(ПакетГитсинк);
	Пакеты.Добавить(ПакетГитрулес);
	Пакеты.Добавить(ПакетПрекоммит);
	
	Возврат Пакеты;
	
КонецФункции

Функция ПакетДляДобавления(Имя, Команды, Использовать = Ложь)
	
	Пакет = Новый Соответствие();
	Пакет.Вставить("Имя", Имя);
	Пакет.Вставить("Использовать", Использовать);
	Пакет.Вставить("Установка", Команды);
	
	Возврат Пакет;
	
КонецФункции

Функция ДанныеПакета(Пакет)
	
	СтруктураПакета = Новый Структура();
	СтруктураПакета.Вставить("Имя", Пакет.Получить("Имя"));
	СтруктураПакета.Вставить("Использовать", Пакет.Получить("Использовать"));
	СтруктураПакета.Вставить("Установка", Пакет.Получить("Установка"));
	
	Возврат СтруктураПакета;
	
КонецФункции

Процедура ВыполнитьКомандыУстановкиПакета(Команды)
	
	Для Каждого СтрокаКоманды Из Команды Цикл
		
		Если НЕ  ВыполнитьКомандуПоСтрокеЗапуска(СтрокаКоманды) Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыполнитьКомандуПоСтрокеЗапуска(СтрокаЗапуска, Параметры = Неопределено)
	
	Команда = Новый Команда;
	
	Если Параметры = Неопределено Тогда
		Команда.УстановитьСтрокуЗапуска(СтрокаЗапуска);
	Иначе
		
		Команда.УстановитьКоманду(СтрокаЗапуска);
		Команда.ДобавитьПараметры(Параметры);
		
	КонецЕсли;
	
	КодВозврата = Команда.Исполнить();
	
	Успешно = КодВозврата = 0;
	
	Если Успешно Тогда
		Лог.Информация(Команда.ПолучитьВывод());
	Иначе
		Лог.Ошибка(Команда.ПолучитьВывод());
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции
