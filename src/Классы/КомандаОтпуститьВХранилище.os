///////////////////////////////////////////////////////////////////////////////////////////////////
//
// Отмена захвата объектов в хранилище 1С.
//
// TODO добавить фичи для проверки команды
//
// Служебный модуль с набором методов работы с командами приложения
//
// Структура модуля реализована в соответствии с рекомендациями
// oscript-app-template (C) EvilBeaver
//
///////////////////////////////////////////////////////////////////////////////////////////////////

#Использовать logos

Перем Лог;

///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт

	ТекстОписания =
		"     Отмена захвата объектов в хранилище 1С.";

	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, ТекстОписания);

	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--storage-name", "Строка подключения к хранилищу");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--storage-user", "Пользователь хранилища");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--storage-pwd", "Пароль");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--objects",	"Имя файла со списком объектов.
	|              Путь к файлу формата XML со списком объектов.
	|              Если опция используется, будет выполнена попытка отменить захват только для объектов, указанных в файле.
	|              Если опция не используется, захват будет отменен для всех объектов конфигурации.
	|              При наличии в списке объектов, не захваченных текущим пользователем или захваченных другим пользователем, ошибка выдана не будет.
	|              Подробнее о формате файла см в документации https://its.1c.ru/db/v8324doc#bookmark:adm:TI000000698 .");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--force", "Игнорировать локальные изменения.
	|              Локально измененные объекты будут получены из хранилища, и внесенные изменения будут потеряны.
	|              Если опция не указана, то при наличии локально измененных объектов операция будет отменена и будет выдана ошибка.");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--do-not-update", "Не обновлять конфигурацию из хранилища.
	|              Значительно ускоряет процесс отмены захвата объектов хранилища. Может вызывать ошибки при использовании 
	|              --objects со списком объектов");

	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры // ЗарегистрироватьКоманду

// Выполняет логику команды
//
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   ДополнительныеПараметры - Соответствие - дополнительные параметры (необязательно)
//
// Возвращаемое значение:
//   Булево - Истина, если команда выполнена успешно
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач ДополнительныеПараметры = Неопределено) Экспорт

	Лог = ДополнительныеПараметры.Лог;

	ДанныеПодключения = ПараметрыКоманды["ДанныеПодключения"];

	МенеджерКонфигуратора = Новый МенеджерКонфигуратора;

	МенеджерКонфигуратора.Конструктор(ДанныеПодключения, ПараметрыКоманды);

Попытка
	
	// сперва получим все изменения
	Если НЕ ПараметрыКоманды["--do-not-update"] Тогда
		МенеджерКонфигуратора.ЗапуститьОбновлениеИзХранилища(
			ПараметрыКоманды["--storage-name"],
			ПараметрыКоманды["--storage-user"],
			ПараметрыКоманды["--storage-pwd"]);
	КонецЕсли;

	МенеджерКонфигуратора.ОтпуститьОбъектыВХранилище(
		ПараметрыКоманды["--storage-name"],
		ПараметрыКоманды["--storage-user"],
		ПараметрыКоманды["--storage-pwd"],
		ПараметрыКоманды["--objects"],
		ПараметрыКоманды["--force"]);

Исключение

	МенеджерКонфигуратора.Деструктор();
	ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

КонецПопытки;

	МенеджерКонфигуратора.Деструктор();

	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;

КонецФункции // ВыполнитьКоманду
