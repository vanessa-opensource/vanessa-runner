///////////////////////////////////////////////////////////////////////////////////////////////////
//
// Обновление конфигурации, находящейся на поддержке
// Объединение текущей конфигурации с файлом (с использованием файла настроек).
//
// Служебный модуль с набором методов работы с командами приложения
//
// Структура модуля реализована в соответствии с рекомендациями
// oscript-app-template (C) EvilBeaver
//
///////////////////////////////////////////////////////////////////////////////////////////////////

#Использовать logos
#Использовать v8runner

Перем Лог;
Перем МенеджерКонфигуратора;

///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт

	ТекстОписания =
		"     Обновление конфигурации, находящейся на поддержке, или объединение конфигурации с файлом (с использованием файла настроек).
		|";

	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды,
		ТекстОписания);

	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--src",
		"Путь файла с объединяемой конфигурацией (.cf-файл) или с файлом обновления конфигурации (.cfu-файл),
		|пример: --src=./1Cv8.cf");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "-s",
		"Краткий псевдоним для команды `--src', пример: -s ./1Cv8.cf");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--update-settings",
		"Файл с настройками объединения конфигураций (обязательный параметр)");
	// Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--enable-support", "Флаг необходимости установки конфигурации на поддержку, если есть возможность поставки на поддержку");
	// Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--disable-support", "Флаг запрета установки конфигурации на поддержку, даже если есть возможность поставки на поддержку");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--IncludeObjectsByUnresolvedRefs",
		"Флаг небходимости включения в объединение объектов,
		|не включенных в список объединяемых и отсутствующих в основной конфигурации");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--ClearUnresolvedRefs",
		"Флаг очистки ссылок на объекты, не включенные в список объединяемых");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--DumpListOfTwiceChangedProperties",
		"Флаг вывода списка всех дважды измененных свойств в файл служебных сообщений 1С.");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--force", "Флаг принудительного обновления");

	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры // ЗарегистрироватьКоманду

// Выполняет логику команды
//
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   ДополнительныеПараметры - Соответствие - дополнительные параметры (необязательно)
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач ДополнительныеПараметры = Неопределено) Экспорт

	Попытка
		Лог = ДополнительныеПараметры.Лог;
	Исключение
		Лог = Логирование.ПолучитьЛог(ПараметрыСистемы.ИмяЛогаСистемы());
	КонецПопытки;

	Лог.Информация("Начинаю объединение конфигураций");

	ДанныеПодключения = ПараметрыКоманды["ДанныеПодключения"];

	ПутьДистрибутива = ОбщиеМетоды.ПолныйПуть(ОбщиеМетоды.ПолучитьПараметры(ПараметрыКоманды, "-s", "--src"));

	ПутьФайлаНастройки = ПараметрыКоманды["--update-settings"];
	// Если Не ЗначениеЗаполнено(ПутьФайлаНастройки) Тогда
	// 	ВызватьИсключение "Необходимо задать к файлу с настройками объединения конфигураций,
	// 	|Параметр --update-settings является обязательным."
	// КонецЕсли;

	ВключитьВОбъединениеОбъектыПоНеразрешеннымСсылкам = ПараметрыКоманды["--IncludeObjectsByUnresolvedRefs"];
	ОчищатьОбъектыПоНеразрешеннымСсылкам = ПараметрыКоманды["--ClearUnresolvedRefs"];
	ВывестиДваждыИзмененныеОбъекты = ПараметрыКоманды["--DumpListOfTwiceChangedProperties"];
	Принудительно = ПараметрыКоманды["--force"];

	Лог.Отладка("ПутьДистрибутива %1", ПутьДистрибутива);
	Лог.Отладка("ПутьФайлаНастройки %1", ПутьФайлаНастройки);
	Лог.Отладка("ВключитьВОбъединениеОбъектыПоНеразрешеннымСсылкам %1", ВключитьВОбъединениеОбъектыПоНеразрешеннымСсылкам);
	Лог.Отладка("ВывестиДваждыИзмененныеОбъекты %1", ВывестиДваждыИзмененныеОбъекты);
	Лог.Отладка("Принудительно %1", Принудительно);

	МенеджерКонфигуратора = Новый МенеджерКонфигуратора;

	Попытка
		МенеджерКонфигуратора.Конструктор(ДанныеПодключения, ПараметрыКоманды);

		УправлениеКонфигуратором = МенеджерКонфигуратора.УправлениеКонфигуратором();

		ПараметрВключитьВОбъединениеОбъектыПоНеразрешеннымСсылкам = Неопределено;
		Если ВключитьВОбъединениеОбъектыПоНеразрешеннымСсылкам Тогда
			ПараметрВключитьВОбъединениеОбъектыПоНеразрешеннымСсылкам = Истина;
		ИначеЕсли ОчищатьОбъектыПоНеразрешеннымСсылкам Тогда
			ПараметрВключитьВОбъединениеОбъектыПоНеразрешеннымСсылкам = Ложь;
		КонецЕсли;

		УправлениеКонфигуратором.ОбновитьКонфигурациюИзФайла(ПутьДистрибутива, ПутьФайлаНастройки,
		ПараметрВключитьВОбъединениеОбъектыПоНеразрешеннымСсылкам, Принудительно, ВывестиДваждыИзмененныеОбъекты);

		Лог.Информация(УправлениеКонфигуратором.ВыводКоманды());

		Лог.Информация("Успешно завершено объединение конфигураций");

	Исключение
		МенеджерКонфигуратора.Деструктор();
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;

	МенеджерКонфигуратора.Деструктор();

	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;
КонецФункции // ВыполнитьКоманду
