///////////////////////////////////////////////////////////////////////////////////////////////////
//
// Обновление конфигурации базы данных в режиме Конфигуратора
//
// TODO добавить фичи для проверки команды
//
// Служебный модуль с набором методов работы с командами приложения
//
// Структура модуля реализована в соответствии с рекомендациями
// oscript-app-template (C) EvilBeaver
//
///////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

Перем Лог; // Экземпляр логгера

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт

	ТекстОписания =
		"     Обновление конфигурации базы данных в режиме Конфигуратора.";

	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды,
		ТекстОписания);
	Парсер.ДобавитьКоманду(ОписаниеКоманды);

	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--v1",
		"Поддержка режима реструктуризации -v1 на сервере");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--v2",
		"Поддержка режима реструктуризации -v2 на сервере");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--dynamic",
		"Динамическое обновление конфигурации");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--ibcmd",
		"Использовать утилиту ibcmd вместо конфигуратора");

КонецПроцедуры // ЗарегистрироватьКоманду

// Выполняет логику команды
//
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   ДополнительныеПараметры - Структура - дополнительные параметры (необязательно)
//
//  Возвращаемое значение:
//   Число - Код возврата команды.
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач ДополнительныеПараметры = Неопределено) Экспорт

	Лог = ОбщиеМетоды.ЛогКоманды(ДополнительныеПараметры);

	РежимРеструктуризации_В1 = ПараметрыКоманды["--v1"];
	РежимРеструктуризации_В2 = ПараметрыКоманды["--v2"];
	ДинамическоеОбновление = ПараметрыКоманды["--dynamic"];

	ДанныеПодключения = ПараметрыКоманды["ДанныеПодключения"];

	МенеджерСборки = ОбщиеМетоды.ФабрикаМенеджераСборки(ПараметрыКоманды);
	МенеджерСборки.Конструктор(ДанныеПодключения, ПараметрыКоманды);

	Лог.Информация("Запускаем обновление конфигурации базы данных...");
	ОбщиеМетоды.ОбновитьКонфигурациюБД(МенеджерСборки, РежимРеструктуризации_В1, РежимРеструктуризации_В2, ДинамическоеОбновление);
	Лог.Информация("Обновление конфигурации базы данных завершено.");

	МенеджерСборки.Деструктор();

	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;

КонецФункции // ВыполнитьКоманду

#КонецОбласти
