///////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды set-version
//
// Структура модуля реализована в соответствии с рекомендациями
// oscript-app-template (C) EvilBeaver
//
///////////////////////////////////////////////////////////////////

#Использовать logos

#Область ОписаниеПеременных

Перем Лог;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт

	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды,
	"     Установка номера версии файлов 1С (конфигурации, расширения, внешние обработки\отчеты).");

	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--new-version",
		"Номер версии, который нужно установить");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--check-module",
		"Исправление версии в модулях объектов внешних обработок\отчетов 1С (Ext/ObjectModule.bsl) или в общих модулях (Ext/Module.bsl)
		|	Формат версии в коде нужно задать в виде отдельной строки Версия = ""1.2.3.4""; // или ""1.2.3""");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--src",
		"Путь Configuration.xml или Configuration.mdo или каталога, содержащего любой из этих файлов,
		|или корневого каталога для поиска в подкаталогах - для исходников конфигурации или расширения
		|Если указан флаг --check-module, тогда в параметре передается путь к указанным модулям или к каталогам\подкаталогам с модулями
		|и будут анализируются только указанные модули кода!");

	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры

// Выполняет логику команды
//
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   ДополнительныеПараметры - Соответствие - дополнительные параметры (необязательно)
//
//  Возвращаемое значение:
//   Произвольный - Успех. См. МенеджерКомандПриложения.РезультатыКоманд()
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач ДополнительныеПараметры) Экспорт

	Лог = ДополнительныеПараметры.Лог;

	НомерВерсии = ПараметрыКоманды["--new-version"];
	ПутьФайла = ПараметрыКоманды["--src"];
	ТолькоМодулиКода = ПараметрыКоманды["--check-module"];

	Если ТолькоМодулиКода Тогда
		Лог.Информация("Изменяю версию в исходниках модуля 1С на %1 - %2", НомерВерсии, ПутьФайла);
	Иначе
		Лог.Информация("Изменяю версию в исходниках конфигурации 1С на %1 - %2", НомерВерсии, ПутьФайла);
	КонецЕсли;

	МенеджерВерсийФайлов1С = Новый МенеджерВерсийФайлов1С;
	СтарыеВерсии = МенеджерВерсийФайлов1С.УстановитьВерсиюКонфигурации(ПутьФайла, НомерВерсии, ТолькоМодулиКода);

	Для каждого КлючЗначение Из СтарыеВерсии Цикл
		ПредыдущаяВерсия = КлючЗначение.Значение;
		Если ТолькоМодулиКода И Не ЗначениеЗаполнено(ПредыдущаяВерсия) Тогда
			Лог.Информация("    Не найдена предыдущая установка версии, новая версия не установлена. Файл - %2",
				ПредыдущаяВерсия, КлючЗначение.Ключ);
		Иначе
			Лог.Информация("    Предыдущая версия %1, файл - %2", ПредыдущаяВерсия, КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;

	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;

КонецФункции

#КонецОбласти
