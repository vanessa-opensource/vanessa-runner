///////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды set-version
//
// Структура модуля реализована в соответствии с рекомендациями
// oscript-app-template (C) EvilBeaver
//
///////////////////////////////////////////////////////////////////

#Использовать logos

#Область ОписаниеПеременных

Перем Лог;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт

	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "     Установка номера версии файлов 1С (конфигурации, расширения, внешние обработки\отчеты).");

	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--new-version",
		"Номер версии, который нужно установить. Внимание - требуется 4х-разрядная версия!");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--for-meta-comment",
		"Версия указана в комментарии внешнего файла");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--src",
		"Путь Configuration.xml или каталога, содержащего этот файл, или корневого каталога для поиска в подкаталогах - для исходников конфигурации или расширения");

	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры

// Выполняет логику команды
//
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   ДополнительныеПараметры - Соответствие - дополнительные параметры (необязательно)
//
//  Возвращаемое значение:
//   Произвольный - Успех. См. МенеджерКомандПриложения.РезультатыКоманд()
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач ДополнительныеПараметры) Экспорт

	Лог = ДополнительныеПараметры.Лог;

	ПутьФайла = ПараметрыКоманды["--src"];
	НомерВерсии = ПараметрыКоманды["--new-version"];
	НастройкиУстановки = Новый Структура;
	НастройкиУстановки.Вставить("ВерсияВКомментарииМетаданного", ПараметрыКоманды["--for-meta-comment"]);

	Лог.Информация("Изменяю версию в исходниках конфигурации или внешнего файла 1С на %1 - %2", НомерВерсии, ПутьФайла);

	МенеджерВерсийФайлов1С = Новый МенеджерВерсийФайлов1С;
	СтарыеВерсии = МенеджерВерсийФайлов1С.УстановитьВерсиюКонфигурации(ПутьФайла, НомерВерсии, НастройкиУстановки);

	Для каждого КлючЗначение Из СтарыеВерсии Цикл
		Лог.Информация("    Старая версия %1, файл - %2", КлючЗначение.Значение, КлючЗначение.Ключ);
	КонецЦикла;

	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;

КонецФункции

#КонецОбласти
