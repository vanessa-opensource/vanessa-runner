///////////////////////////////////////////////////////////////////
//
// Служебный модуль с набором методов работы с командами приложения
//
// Структура модуля реализована в соответствии с рекомендациями
// oscript-app-template (C) EvilBeaver
//
///////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт

	ТекстОписания =
		"     Сборка внешних обработок из исходников штатно через выгрузку 1С 8.3.";

	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, ТекстОписания);

	Парсер.ДобавитьПозиционныйПараметрКоманды(ОписаниеКоманды, "inputPath", "Путь к каталогу или внешней обработке
	|
	|Схема работы:
	|		В каталоге загрузки создается отдельный подкаталог для каждой внешней обработки
	|		Сохраняется структура подкаталогов, если загружается каталог.
	|");
	Парсер.ДобавитьПозиционныйПараметрКоманды(ОписаниеКоманды, "outputPath", "Путь бинарников");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--cachekey",
		"Ключ кэшированных значений файлов, default: compileepfrunner");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--onlycopy",
		"Флаг, только копировать. Без сборки обработки, даже если выглядит как обработка");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--build-number",
		"Номер сборки для установки в последний разряд номера версии");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "--for-meta-comment",
		"Версия указана в комментарии внешнего файла");

	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры

// Выполняет логику команды
//
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   ДополнительныеПараметры - Соответствие - дополнительные параметры (необязательно)
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач ДополнительныеПараметры = Неопределено) Экспорт

	УпаковщикВнешнихОбработок = Новый УпаковщикВнешнихОбработок;
	УпаковщикВнешнихОбработок.УстановитьЛог(ДополнительныеПараметры.Лог);

	ДанныеПодключения = ПараметрыКоманды["ДанныеПодключения"];
	НастройкаИзмененияВерсии = Новый Структура;
	НастройкаИзмененияВерсии.Вставить("НомерСборки", ПараметрыКоманды["--build-number"]);
	НастройкаИзмененияВерсии.Вставить("ВерсияВКомментарииМетаданного", ПараметрыКоманды["--for-meta-comment"]);
	Если ЗначениеЗаполнено(НастройкаИзмененияВерсии.НомерСборки) И Не НастройкаИзмененияВерсии.ВерсияВКомментарииМетаданного Тогда
		ВызватьИсключение "При указании номера сборки нужно указывать флаг ""--for-meta-comment"", т.к. других вариантов установки версии пока не реализовано."
	КонецЕсли;

	УпаковщикВнешнихОбработок.Собрать(
		ОбщиеМетоды.ПолныйПуть(ПараметрыКоманды["inputPath"]),
		ОбщиеМетоды.ПолныйПуть(ПараметрыКоманды["outputPath"]),
		ДанныеПодключения,
		ПараметрыКоманды["--onlycopy"], ПараметрыКоманды["--cachekey"],
		НастройкаИзмененияВерсии);

	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;
КонецФункции
