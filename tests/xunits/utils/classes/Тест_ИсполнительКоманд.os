#Использовать "..\..\..\.."
#Использовать cmdline

#Область ОписаниеПеременных

Перем Команда;
Перем Аргументы;
Перем СпособВывода;
Перем КаталогКоманды;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПараметр(ИмяПараметра, ЗначениеПараметра) Экспорт
	Аргументы.Добавить(ИмяПараметра);
	Аргументы.Добавить(ЗначениеПараметра);
КонецПроцедуры

Процедура ДобавитьФлаг(ИмяФлага) Экспорт
	Аргументы.Добавить(ИмяФлага);
КонецПроцедуры

Процедура ВыполнитьКоманду() Экспорт

	СистемнаяИнформация = Новый СистемнаяИнформация;
	ПараметрыСистемы.ЭтоWindows = Найти(ВРег(СистемнаяИнформация.ВерсияОС), "WINDOWS") > 0;
	
	ДобавитьПараметр("--root", КаталогКоманды);
	ДобавитьФлаг("--nocacheuse");

	МенеджерКомандПриложения.РегистраторКоманд(ПараметрыСистемы);
	
	ДобавитьСпособВывода(МенеджерКомандПриложения);

	Парсер = Новый ПарсерАргументовКоманднойСтроки();
	МенеджерКомандПриложения.ЗарегистрироватьКоманды(Парсер);
	ПараметрыКоманды = Парсер.Разобрать(Аргументы);
	МенеджерКомандПриложения.ВыполнитьКоманду(Команда, ПараметрыКоманды.ЗначенияПараметров);

КонецПроцедуры

Функция ЛогКоманды() Экспорт
	Возврат СпособВывода;
КонецФункции

Функция КаталогКоманды() Экспорт
	Возврат КаталогКоманды;
КонецФункции

Функция ПутьТестовыхДанных(Путь1, Путь2 = Неопределено, Путь3 = Неопределено) Экспорт

	КаталогШаблонов = ОбъединитьПути(ТекущийКаталог(), "tests", "fixtures");
	Возврат ОбъединитьПути(КаталогШаблонов, Путь1, Путь2, Путь3);

КонецФункции

Процедура ОжидаемЧтоВыводСодержит(Строка) Экспорт
	Ожидаем.Что(СпособВывода.ВыводЛога()).Содержит(Строка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта(КомандаПриложения) 

	Команда = КомандаПриложения;
	Аргументы = Новый Массив;
	Аргументы.Добавить(Команда);

	КаталогКоманды = ВременныеФайлы.СоздатьКаталог();

	СпособВывода = Новый Тест_ВыводЛога();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСпособВывода(МенеджерКомандПриложения) 
	Лог = МенеджерКомандПриложения.Лог();
	Лог.ДобавитьСпособВывода(СпособВывода);
КонецПроцедуры

#КонецОбласти
