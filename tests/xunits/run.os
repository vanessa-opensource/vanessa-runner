#Использовать asserts
#Использовать tempfiles
#Использовать "utils"
#Использовать "..\.."

#Область ОписаниеПеременных

Перем НакопленныеВременныеФайлы; // фиксация накопленных времнных файлов для сброса

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&Тест
Процедура ТестДолжен_ЗапуститьОбработкуЗакрытьПредприятиеIbsrv() Экспорт

	// Дано
	Исполнитель = Новый Тест_ИсполнительКоманд("run");
	Исполнитель.УстановитьКонтекстПустаяИБ();
	Исполнитель.ДобавитьПараметр("--execute", "$runnerRoot/epf/ЗакрытьПредприятие.epf");
	Исполнитель.ДобавитьФлаг("--ibsrv");
	
	// Когда
	Исполнитель.ВыполнитьКоманду();

	// Тогда
	Исполнитель.ОжидаемЧтоВыводСодержит("Используется ibsrv");
	Исполнитель.ОжидаемЧтоВыводСодержит("Выполнение команды/действия в режиме 1С:Предприятие завершено.");
		
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСНевернымИменемПользователяIbsrv() Экспорт

	// Дано
	Исполнитель = Новый Тест_ИсполнительКоманд("run");
	Исполнитель.УстановитьКонтекстПустаяИБ();
	Исполнитель.ДобавитьПараметр("--execute", "$runnerRoot/epf/ЗакрытьПредприятие.epf");
	Исполнитель.ДобавитьФлаг("--ibsrv");
	
	// Когда
	Исполнитель.ДобавитьПараметр("--db-user", "НеизвестныйПользователь");

	// Тогда
	Ожидаем.Что(Исполнитель)
		.Метод("ВыполнитьКоманду")
		.ВыбрасываетИсключение("Пользователь ИБ не идентифицирован");
	Исполнитель.ОжидаемЧтоВыводСодержит("Используется ibsrv");
		
КонецПроцедуры

&Тест
Процедура ТестДолжен_ЗапуститьСОткрытиемНавигационнойСсылкиIbsrv() Экспорт

	// Дано
	Исполнитель = Новый Тест_ИсполнительКоманд("run");
	Исполнитель.УстановитьКонтекстПустаяИБ();
	Исполнитель.ДобавитьПараметр("--execute", "$runnerRoot/epf/ЗакрытьПредприятие.epf");
	Исполнитель.ДобавитьПараметр("--url", "e1cib/navigationpoint/startpage");
	Исполнитель.ДобавитьФлаг("--ibsrv");
	
	// Когда
	Исполнитель.ВыполнитьКоманду();

	// Тогда
	Исполнитель.ОжидаемЧтоВыводСодержит("Используется ibsrv");
	Исполнитель.ОжидаемЧтоВыводСодержит("Выполнение команды/действия в режиме 1С:Предприятие завершено.");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗапускомТеста() Экспорт
	
	НакопленныеВременныеФайлы = ВременныеФайлы.Файлы();
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
	ВременныеФайлы.УдалитьНакопленныеВременныеФайлы(НакопленныеВременныеФайлы);
	
КонецПроцедуры

#КонецОбласти
