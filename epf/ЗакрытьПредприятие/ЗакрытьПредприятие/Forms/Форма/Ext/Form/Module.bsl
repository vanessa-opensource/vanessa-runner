&НаКлиенте
Перем СчетчикОжиданияРезультатов;

&НаКлиенте
Перем МаксИтерацийОжиданияРезультатов;

&НаКлиенте
Перем ИндикаторВыполнения;

&НаКлиенте
Перем ФормаОбновленияНайденаОдинРаз;

&НаКлиенте
Перем ФормаНачальногоЗаполненияНайденаОдинРаз;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОшибкаОбновления = Ложь;
	
	Попытка
		Выполнить("ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(""ОбщиеНастройкиПользователя"", 
			|""ЗапрашиватьПодтверждениеПриЗавершенииПрограммы"", Ложь);");
	Исключение
		// Данного модуля и метода может не быть в конфигурации
	КонецПопытки;   
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЭтоКонфигурацияНаБазеБСПСервер() Тогда
		ПрекратитьРаботуСистемы();
	КонецЕсли;  
	
	ОпределитьПутиИзПараметраЗапуска();
	Если Не ПустаяСтрока(ПутьКФайлуРезультата) Тогда 
		УдалитьФайлБезопасно(ПутьКФайлуРезультата);
		Сообщить("Путь файла лога " + ПутьКФайлуРезультата);
	КонецЕсли;   
	Если Не ПустаяСтрока(ПутьКФайлуСвойствКонфигурации) Тогда
		УдалитьФайлБезопасно(ПутьКФайлуСвойствКонфигурации);
		Сообщить("Путь файла свойств конфигурации " + ПутьКФайлуСвойствКонфигурации);
		ЗаписатьСвойстваКонфигурации();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ПроверитьНеобходимостьЗавершенияПрограммы", 10, Истина);
	ПодключитьОбработчикОжидания("ПроверитьЛегальностьОбновления", 2);
	Если СуществуетПодсистемаМультиязычность() Тогда
		ПодключитьОбработчикОжидания("ПроверитьРегиональныеНастройки", 2);
	    ПроверитьРегиональныеНастройки();
	КонецЕсли;	 
	
	ПроверитьНеобходимостьЗавершенияПрограммы();
	ПроверитьЛегальностьОбновления(); 
	
КонецПроцедуры  

&НаКлиенте
Процедура ОпределитьПутиИзПараметраЗапуска() 
	
	ОписанияПутей = Новый Структура;
	ОписанияПутей.Вставить("ПутьКФайлуРезультата", "exitCodePath");
	ОписанияПутей.Вставить("ПутьКФайлуСвойствКонфигурации", "configPropsPath"); 
	
	ПараметрыЗапуска = СтрРазделить(ПараметрЗапуска, ";", Ложь);
	Для Каждого ЧастьПараметраЗапуска из ПараметрыЗапуска Цикл
		
		Для Каждого ЭлементОписанияПути Из ОписанияПутей Цикл   
			
			Если Не ПустаяСтрока(ЭтаФорма[ЭлементОписанияПути.Ключ]) Тогда 
				Продолжить;
			КонецЕсли;  
			
			СтрокаПоиска = ЭлементОписанияПути.Значение + "="; 
			ПозицияКлючаПути = СтрНайти(ВРег(ЧастьПараметраЗапуска), ВРег(СтрокаПоиска));
			
			Если ПозицияКлючаПути > 0 Тогда  
				ПутьКФайлу = Сред(ЧастьПараметраЗапуска, ПозицияКлючаПути + СтрДлина(СтрокаПоиска)); 
				Если СтрНачинаетсяС(ПутьКФайлу, """") Тогда 
					ПутьКФайлу = Сред(ПутьКФайлу, 2);
				КонецЕсли;
				Если СтрЗаканчиваетсяНа(ПутьКФайлу, """") Тогда 
					ПутьКФайлу = Лев(ПутьКФайлу, СтрДлина(ПутьКФайлу) - 1);
				КонецЕсли;
				ЭтаФорма[ЭлементОписанияПути.Ключ] = ПутьКФайлу;
				Прервать;
			КонецЕсли;	                           
			
		КонецЦикла;	 
		
	КонецЦикла;   
	
КонецПроцедуры  

&НаКлиенте
Процедура ЗаписатьСвойстваКонфигурации()  
	
	Попытка
		ТекстовыйФайл = Новый ТекстовыйДокумент();
		ТекстовыйФайл.УстановитьТекст(ТекстСвойствКонфигурации());
		ТекстовыйФайл.Записать(ПутьКФайлуСвойствКонфигурации, КодировкаТекста.UTF8);
	Исключение   
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьВЖурналРегистрации(ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры   

&НаСервереБезКонтекста
Функция ТекстСвойствКонфигурации()  
	
	ЗаписываемыеСвойства = Новый Структура;  
	
	// Заполним часть свойств из метаданных конфигурации
	ЗаписываемыеСвойства.Вставить("АвторскиеПрава");
	ЗаписываемыеСвойства.Вставить("АдресИнформацииОКонфигурации");
	ЗаписываемыеСвойства.Вставить("АдресИнформацииОПоставщике");
	ЗаписываемыеСвойства.Вставить("АдресКаталогаОбновлений");
	ЗаписываемыеСвойства.Вставить("ВариантВстроенногоЯзыка");
	ЗаписываемыеСвойства.Вставить("Версия");
	ЗаписываемыеСвойства.Вставить("Имя");
	ЗаписываемыеСвойства.Вставить("ИспользоватьОбычныеФормыВУправляемомПриложении");
	ЗаписываемыеСвойства.Вставить("ИспользоватьУправляемыеФормыВОбычномПриложении");
	ЗаписываемыеСвойства.Вставить("Комментарий");
	ЗаписываемыеСвойства.Вставить("КраткаяИнформация");
	ЗаписываемыеСвойства.Вставить("Поставщик");

	ЗаполнитьЗначенияСвойств(ЗаписываемыеСвойства, Метаданные);  
	
	// Дополним свойства другими данными
	ЗаписываемыеСвойства.Вставить("КонфигурацияИзменена", КонфигурацияИзменена());
	
	// Почистим записываемые данные
	ПустыеКлючи = Новый Массив; 
	ДопустимыеТипы = Новый ОписаниеТипов("Булево, Строка, Дата, Число");
	
	Для Каждого ЭлементСвойств Из ЗаписываемыеСвойства Цикл 
		Если ЭлементСвойств.Значение = Неопределено Тогда 
			ПустыеКлючи.Добавить(ЭлементСвойств.Ключ);
			Продолжить;
		КонецЕсли;
		Если Не ДопустимыеТипы.СодержитТип(ТипЗнч(ЭлементСвойств.Значение)) Тогда 
			ЗаписываемыеСвойства.Вставить(ЭлементСвойств.Ключ, Строка(ЭлементСвойств.Значение));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ПустойКлюч Из ПустыеКлючи Цикл 
		ЗаписываемыеСвойства.Удалить(ПустойКлюч);
	КонецЦикла;   
	
	// Сериализуем свойства в JSON
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ЗаписываемыеСвойства); 
	ТекстСвойствКонфигурации = ЗаписьJSON.Закрыть();
	
	Возврат ТекстСвойствКонфигурации;
	
КонецФункции

&НаКлиенте 
Процедура ПроверитьНеобходимостьЗавершенияПрограммы() Экспорт
	
	НеобходимоОжидание = Найти(ПараметрЗапуска, "ЗавершитьРаботуСистемы") > 0;
	МожноЗавершатьРаботу = Ложь;
	СтрокаНеудачиОбновления = Нрег(НСтр("ru='Не удалось выполнить обновлени';uk='Не вдалося виконати оновленн'"));
	
	СтрокаНачальноеЗаполнение = НСтр("ru='начальное заполнение';uk='початкове заповнення'");
	СтрокаПереходСДругойПрограммы = НСтр("ru='переход';uk='переход'");
	СтрокаНевыполненыДополнительныеПроцедуры = НСтр("ru='не выполнены дополнительные процедуры'");
	СтрокаОбновлениеВерсии = НСтр("ru='обновление версии';uk='оновлення версії'" );
	СтрокаЧтоНового = НРег(НСтр("ru='Что нового в конфигурации';uk='Що нового в конфігурації'"));

	ФормаОбновленияНайдена = Ложь;
	ФормаНачальногоЗаполненияНайдена = Ложь;
	
	Окна = ПолучитьОкна();
	Для каждого Окн Из Окна Цикл
		Если ТипЗнч(Окн) = Тип("ОкноКлиентскогоПриложения") Тогда
			
			Содержимое = Окн.ПолучитьСодержимое();
			ЗаголовокНРег = НРег(Строка(Окн.Заголовок));
			
			ОбновитьПрогресс = Ложь;
			Если Найти(ЗаголовокНРег, СтрокаОбновлениеВерсии) > 0 Тогда
				ФормаОбновленияНайденаОдинРаз = Истина;
				ФормаОбновленияНайдена = Истина;
				ОбновитьПрогресс = Истина;
				
			ИначеЕсли Найти(ЗаголовокНРег, СтрокаНачальноеЗаполнение) > 0 Тогда

				ФормаНачальногоЗаполненияНайденаОдинРаз = Истина;
				ФормаНачальногоЗаполненияНайдена = Истина;
				ОбновитьПрогресс = Истина;
			ИначеЕсли Найти(ЗаголовокНРег, СтрокаПереходСДругойПрограммы) > 0 Тогда

				ФормаНачальногоЗаполненияНайденаОдинРаз = Истина;
				ФормаНачальногоЗаполненияНайдена = Истина;
				ОбновитьПрогресс = Истина;
			ИначеЕсли Найти(ЗаголовокНРег, СтрокаНевыполненыДополнительныеПроцедуры) > 0 Тогда
				
				ФормаОбновленияНайдена = Истина;
				
			КонецЕсли;
			
			Если ОбновитьПрогресс Тогда
				
				СчетчикОжиданияРезультатов = 1;
				ОбновитьПрогресс(Содержимое, ИндикаторВыполнения);
				
			КонецЕсли;
			
			Если Найти(ЗаголовокНРег, СтрокаНевыполненыДополнительныеПроцедуры)>0 Тогда 
				Если Не Содержимое = Неопределено И ТипЗнч(Содержимое) = Тип("УправляемаяФорма") Тогда
					Содержимое.Закрыть(Истина);
					МожемЗавершатьРаботу = Ложь;
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
			Если СчетчикОжиданияРезультатов > 0 И Найти(ЗаголовокНРег, СтрокаЧтоНового)>0 Тогда
				СчетчикОжиданияРезультатов = МаксИтерацийОжиданияРезультатов + 1;
				Сообщить(""+ТекущаяДата() + " - Удачное завершение обновления");
				Прервать;
			КонецЕсли;
			
			Если Найти(ЗаголовокНРег, СтрокаНеудачиОбновления)>0 Тогда 
			    СчетчикОжиданияРезультатов = МаксИтерацийОжиданияРезультатов + 1;
				МожноЗавершатьРаботу = Истина;
				ОшибкаОбновления = Истина;
				
				Попытка
					Если ТипЗнч(Содержимое) = Тип("УправляемаяФорма") Тогда
						ТекстОшибки = Содержимое.Элементы.ТекстСообщенияОбОшибке.Заголовок;
						Сообщить("ERROR: "+ТекущаяДата() + " "+ТекстОшибки);
					КонецЕсли; 
					
				Исключение
				 	Сообщить("ERROR: "+ТекущаяДата() + ОписаниеОшибки());   
				КонецПопытки; 
				
				Сообщить("ERROR: "+ТекущаяДата() + "Неудачное обновление конфигурации");
				Если Не Содержимое = Неопределено И ТипЗнч(Содержимое) = Тип("УправляемаяФорма") Тогда
					Содержимое.Закрыть();
				КонецЕсли;
				
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НеобходимоОжидание И Не МожноЗавершатьРаботу И СчетчикОжиданияРезультатов <= МаксИтерацийОжиданияРезультатов Тогда
		СчетчикОжиданияРезультатов = СчетчикОжиданияРезультатов + 1;
		ПодключитьОбработчикОжидания("ПроверитьНеобходимостьЗавершенияПрограммы", 10, Истина); 
		Если СчетчикОжиданияРезультатов <= МаксИтерацийОжиданияРезультатов Тогда 
			Сообщить("Ожидание при завершении программы. Итерация " + 
				СчетчикОжиданияРезультатов + " из " + МаксИтерацийОжиданияРезультатов);
		КонецЕсли;
	КонецЕсли; 
	
	МожемЗавершатьРаботу = Ложь;
	Если Не ФормаОбновленияНайдена И ФормаОбновленияНайденаОдинРаз Тогда
		МожемЗавершатьРаботу = Истина;
	ИначеЕсли Не ФормаНачальногоЗаполненияНайдена И ФормаНачальногоЗаполненияНайденаОдинРаз Тогда
		МожемЗавершатьРаботу = Истина;
	ИначеЕсли СчетчикОжиданияРезультатов > МаксИтерацийОжиданияРезультатов Тогда
		МожемЗавершатьРаботу = Истина;
	КонецЕсли; 
	
	Если МожемЗавершатьРаботу И НеобходимоОжидание Тогда
		Сообщить("" + ТекущаяДата() + " - " + "Завершаем работу");
		ПодключитьОбработчикОжидания("ЗавершитьРаботу", 1, Истина);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПрогресс(Знач Содержимое, ИндикаторВыполнения)
	ПроцентВыполнения = 0;
	Попытка
		Если ТипЗнч(Содержимое) = Тип("УправляемаяФорма") Тогда
			ПроцентВыполнения = Содержимое.ПрогрессВыполнения;
		КонецЕсли; 
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки; 
	
	Если ПроцентВыполнения <> ИндикаторВыполнения Тогда
		ИндикаторВыполнения = ПроцентВыполнения;
		СтрокаСообщения = ""+ТекущаяДата() + " - " + ПроцентВыполнения + "% Нашли форму обновления подождем еще";
		Сообщить(СтрокаСообщения);
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьПодтверждениеЛегальностиПолученияОбновлений()
	
	УстановитьПривилегированныйРежим(Истина);
	Выполнить("ОбновлениеИнформационнойБазыСлужебный.ЗаписатьПодтверждениеЛегальностиПолученияОбновлений();");
		
КонецПроцедуры
 
&НаКлиенте 
Процедура ПроверитьЛегальностьОбновления() Экспорт
	СтрокаЛегальност = НРег(НСтр("ru='легальност';uk='легальніст'"));

	Окна = ПолучитьОкна();
	Для каждого Окн Из Окна Цикл
		Если ТипЗнч(Окн) = Тип("ОкноКлиентскогоПриложения") Тогда
			Содержимое = Окн.ПолучитьСодержимое();
			Если Найти(НРег(Строка(Окн.Заголовок)), СтрокаЛегальност) > 0 Тогда
				Попытка
					Если ТипЗнч(Содержимое) = Тип("УправляемаяФорма") Тогда
						Содержимое.Результат = Истина;  
						Содержимое.Закрыть(Истина);
						ЗаписатьПодтверждениеЛегальностиПолученияОбновлений();
						СчетчикОжиданияРезультатов = 0;
					КонецЕсли; 
				Исключение
				    Сообщить(ОписаниеОшибки());
				КонецПопытки;
				
				ОтключитьОбработчикОжидания("ПроверитьЛегальностьОбновления");
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
				
КонецПроцедуры
	
&НаКлиенте
Процедура ЗавершитьРаботу() Экспорт 
	Если Не ОшибкаОбновления Тогда
		ЗаписатьФайлРезультата();
	КонецЕсли;
	ПрекратитьРаботуСистемы();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоКонфигурацияНаБазеБСПСервер()
	Рез = Ложь;
	Описание = Новый Структура;
	Попытка
		Выполнить("ОбновлениеИнформационнойБазыБСП.ПриДобавленииПодсистемы(Описание);");
		Рез = Описание.Имя = "СтандартныеПодсистемы";
	Исключение
		ИнфоОшибки = ИнформацияОбОшибке();
		Рез = НРег(ИнфоОшибки.ИмяМодуля) = НРег("ОбщийМодуль.ОбновлениеИнформационнойБазыБСП.Модуль");
	КонецПопытки;
	Возврат Рез;
КонецФункции

&НаКлиенте
Процедура ЗаписатьФайлРезультата()
	
	Если ПутьКФайлуРезультата = "" Тогда
		Возврат;
	КонецЕсли; 
	
	Попытка
		ТекстовыйФайл = Новый ТекстовыйДокумент;
		ТекстовыйФайл.УстановитьТекст("0");
		ТекстовыйФайл.Записать(ПутьКФайлуРезультата, КодировкаТекста.UTF8); 
	Исключение
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьВЖурналРегистрации(ТекстСообщения);
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлБезопасно(ПутьКФайлу = Неопределено)
	
	Если ПустаяСтрока(ПутьКФайлу) Тогда
		Возврат;
	КонецЕсли; 
	
	Попытка
		УдалитьФайлы(ПутьКФайлу);
	Исключение
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьВЖурналРегистрации(ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте 
Процедура ПроверитьРегиональныеНастройки() Экспорт
	СтрокаРегиональныеНастройки = НРег(НСтр("ru='Региональные настройки'"));

	Окна = ПолучитьОкна();
	Для каждого Окн Из Окна Цикл
		Если ТипЗнч(Окн) = Тип("ОкноКлиентскогоПриложения") Тогда
			Содержимое = Окн.ПолучитьСодержимое();
			Если Найти(НРег(Строка(Окн.Заголовок)), СтрокаРегиональныеНастройки) > 0 Тогда
				Попытка
					Если ТипЗнч(Содержимое) = Тип("УправляемаяФорма") Тогда  
						Содержимое.Закрыть(Новый Структура("Отказ", Ложь));;
						СчетчикОжиданияРезультатов = 0;
					КонецЕсли; 
				Исключение
				    Сообщить(ОписаниеОшибки());
				КонецПопытки;
				
				ОтключитьОбработчикОжидания("ПроверитьРегиональныеНастройки");
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
				
КонецПроцедуры

&НаКлиенте
Функция СуществуетПодсистемаМультиязычность()
	Рез = Ложь;
	Попытка
		Выполнить("Рез = ОбщегоНазначенияКлиент.ПодсистемаСуществует(""СтандартныеПодсистемы.Мультиязычность"");");
	Исключение
	КонецПопытки;
	Возврат Рез;
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьВЖурналРегистрации(Комментарий);
	ЗаписьЖурналаРегистрации(КлючЖР(), УровеньЖурналаРегистрации.Ошибка, Неопределено, Неопределено, Комментарий);
КонецПроцедуры

&НаСервереБезКонтекста
Функция КлючЖР() 
	Возврат "VanessaRunner.ЗакрытьПредприятие";	
КонецФункции

СчетчикОжиданияРезультатов = 0;
МаксИтерацийОжиданияРезультатов = 5;
ИндикаторВыполнения = 0;
ФормаОбновленияНайденаОдинРаз = Ложь;
ФормаНачальногоЗаполненияНайденаОдинРаз = Ложь;
